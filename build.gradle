//-------------------------------------------------------------------------------------------------
// GRADLE PLUGINS.
//-------------------------------------------------------------------------------------------------

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

// print welcome message.
apply from: rootProject.file("gradle/welcome.gradle")

// task to add gradle wrapper files.
task("wrapper", type: Wrapper, group: "build setup") {
    gradleVersion = "4.9"
    distributionUrl = "https://services.gradle.org/distributions/" +
            "gradle-${gradleVersion}-all.zip"
}

// task to print gradle and groovy versions.
task("versions", group: "help").doLast {
    println "Java version: ${System.properties["java.version"]}"
    println "Gradle version: ${gradle.gradleVersion}"
    println "Groovy version: ${GroovySystem.version}"
}

// task to create main and test source directories.
task("initSourceDirs", group: "build setup").doLast {
    // ignore source directories for projects without source sets.
    if (!project.hasProperty("sourceSets")) { return }

    // list all source directories.
    def sourceSets = project.sourceSets as SourceSetContainer
    def sourceDirs = sourceSets*.allSource.srcDirs.flatten() as List<File>

    // create source directories, for those who not exists.
    for (sourceDir in sourceDirs) { sourceDir.mkdirs() }
}

ext.openjfxVersion = '11-ea+19'

static def getOSName() {
    final String osName = System.getProperty("os.name").toLowerCase()
    if (osName.contains("linux")) {
        return ("linux")
    } else if (osName.contains("mac os x") || osName.contains("darwin") || osName.contains("osx")) {
        return ("mac")
    } else if (osName.contains("windows")) {
        return ("win")
    }
    return ""
}

ext.platform = getOSName()

// provide java tasks.
apply plugin: "java"

repositories {
    jcenter()
}

dependencies {
    compile "org.openjfx:javafx-base:${openjfxVersion}:${platform}"
    compile "org.openjfx:javafx-controls:${openjfxVersion}:${platform}"
    compile "org.openjfx:javafx-graphics:${openjfxVersion}:${platform}"
    compile "org.openjfx:javafx-swing:${openjfxVersion}:${platform}"
    compile "org.openjfx:javafx-fxml:${openjfxVersion}:${platform}"
}

// java language level.
sourceCompatibility = "10"
targetCompatibility = "10"

// configure publish tasks.
apply from: rootProject.file("gradle/publish-bintray.gradle")

// task to create jar with source code.
task("sourceJar", type: Jar) {
    group "Build"
    description "An archive of the source code"
    classifier "sources"
    from sourceSets.main.allJava
}

// task to create jar with javadocs.
task("javadocJar", type: Jar) {
    group "Build"
    description "An archive of the javadoc"
    classifier "javadoc"
    from javadoc
}

jar.finalizedBy sourceJar
jar.finalizedBy javadocJar

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

compileJava {
    options.compilerArgs += [
            "--add-opens", "java.base/java.lang=ALL-UNNAMED",
            "--add-opens", "java.base/java.util=ALL-UNNAMED",
            "--add-exports", "javafx.graphics/com.sun.glass.ui=ALL-UNNAMED",
            "--add-exports", "javafx.graphics/com.sun.glass.events=ALL-UNNAMED",
            "--add-exports", "javafx.graphics/com.sun.glass.ui.delegate=ALL-UNNAMED",
            "--add-exports", "javafx.graphics/com.sun.glass.utils=ALL-UNNAMED",
            "--add-exports", "javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED"
    ]

    javadoc {
        options.addStringOption("Xdoclint:none", "-quiet")
        options.addMultilineStringsOption('-add-exports').setValue(['javafx.graphics/com.sun.glass.ui=ALL-UNNAMED',
        'javafx.graphics/com.sun.glass.events=ALL-UNNAMED',
        'javafx.graphics/com.sun.glass.ui.delegate=ALL-UNNAMED',
        'javafx.graphics/com.sun.glass.utils=ALL-UNNAMED',
        'javafx.graphics/com.sun.javafx.tk=ALL-UNNAMED',])
    }
}

ext {
    def buildTimeAndDate = new Date()
    buildDate = buildTimeAndDate.format("yyyy-MM-dd")
    buildTime = buildTimeAndDate.format("HH:mm:ss.SSSZ")

    javaVersion = System.properties["java.version"]
    javaVendor = System.properties["java.vendor"]
    javaVmVersion = System.properties["java.vm.version"]
}

jar {
    manifest.attributes(
        "Created-By": project.javaVersion +
            " (" + project.javaVendor + " " + project.javaVmVersion + ")",
        "Build-Date": project.buildDate,
        "Build-Time": project.buildTime,
        "Specification-Title": project.name,
        "Specification-Version": project.version,
        "Specification-Vendor": project.vendor,
        "Implementation-Title": project.name,
        "Implementation-Version": project.version,
        "Implementation-Vendor": project.vendor
    )
}

