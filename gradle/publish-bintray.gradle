apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

if (!project.hasProperty("bintrayUsername")) ext.bintrayUsername = "invalid"
if (!project.hasProperty("bintrayApiKey")) ext.bintrayApiKey = "invalid"

def pomConfig = {
    name project.name
    description project.description
    url project.url

    licenses {
        license([:]) {
            name project.licenseName
            url project.licenseUrl
            distribution "repo"
        }
    }

    developers {
        developer { name project.vendor }
    }

    scm {
        url project.sourceUrl
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact sourceJar
                artifact javadocJar
                pom.withXml {
                    asNode().dependencies.'*'
                            .findAll() { it.groupId.text() == 'org.openjfx' }
                            .each { it.remove(it.classifier) }
                    asNode().appendNode("description", pomConfig.description)
                    asNode().children().last() + pomConfig

                }
            }
        }
    }
}

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ["mavenJava"]

    dryRun = false

    pkg {
        userOrg = project.bintrayOrg
        repo = project.bintrayRepo
        name = project.name
        desc = project.description

        licenses = [project.licenseNameShort]
        labels = []

        websiteUrl = project.url
        issueTrackerUrl = project.issuesUrl
        vcsUrl = project.sourceUrl

        publicDownloadNumbers = true

        version {
            vcsTag = project.version
        }
    }
}
